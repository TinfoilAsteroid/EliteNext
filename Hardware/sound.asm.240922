
;-------------------------------------------------------------------------------------------------
;--- Equates for sounds
;       DEFINE          USETIMER 1
                        IFDEF   USETIMER
SOUNDSTEPLENGTH             EQU     25
                        ENDIF
  

WriteTurboControlA:     MACRO   
                        ld      bc,TURBO_SOUND_NEXT_CONTROL
                        out     (c),a
                        ENDM
                        
WriteTurboRegisterA:    MACRO   value
                        WriteTurboControlA
                        ld      b,$BF
                        ld      a,value
                        out     (c),a
                        ENDM
                        
WriteTurboRegister:     MACRO   register,value
                        ld      a,register
                        WriteTurboControlA
                        ld      b,$BF
                        ld      a,value
                        out     (c),a
                        ENDM

WriteAToTurboRegister:  MACRO   register
                        ex      af,af'
                        ld      a,register
                        WriteTurboControlA
                        ld      b, $BF
                        ex      af,af'
                        out     (c),a
                        ENDM

SelectAY:               MACRO   chipNbr
                        ld      a,TURBO_MANDATORY | TURBO_LEFT | TURBO_RIGHT | chipNbr
                        WriteTurboControlA
                        ENDM

;--- Interrupt handler, moved from main code
SoundInterruptHandler:  ld      a,(DELTA)
                        ld      hl,LAST_DELTA
                        cp      (hl)
.SpeedChange:           call    nz, UpdateEngineSound
.NoSpeedChange:         ld      a,(SoundFxToEnqueue)        ; Check for new sound 
                        cp      $FF
                        call    nz,EnqueSound
.NoNewSound:            IFDEF   USETIMER
                            ld      hl,SoundChannelTimer
                        ENDIF
                        ld      de,SoundChannelSeq
                        ld      b,8
.ResetLoop:             ld      a,(de)                  ; we only update active channels
                        cp      $FF
                        jr      z,.NextCounter
                        IFDEF   USETIMER
                            dec     (hl)                    ; so update channel timer
                            jr      nz,.NextCounter         ; if its not zero then continue
                        ENDIF
                        ld      a,8                     ; a now = channel to play
                        sub     a,b
                        IFDEF   USETIMER
                            push    bc,,de,,hl              ; save state
                        ELSE
                            push    bc,,de
                        ENDIF
                        call    PlaySound               ; play sound
                        IFDEF   USETIMER
                            pop     bc,,de,,hl              ; restore state so de = correct timer & hl = correct channel, b = coutner
                        ELSE
                            pop     bc,,de              ; restore state so de = correct timer & hl = correct channel, b = coutner
                        ENDIF                
; If it went negative new sound update
                        IFDEF   USETIMER
.ResetTimer:                ld      a,SOUNDSTEPLENGTH       ; as we fallin to this it will auto update counter 
                            ld      (hl),a                  ; so may take it out of playsound routine
                        ENDIF
.NextCounter:       
                        IFDEF   USETIMER
                            inc     hl
                        ENDIF
                        inc     de
                        djnz    .ResetLoop
                        ret


; On entering here a = AY chip to select
DefaultAYChip:          ld      hl,$FFBF            ; h = turbo control, l = turbo register
                        ld      c,$FD               ; bc = h$FD or l$FD
                        ld      de,$000E            ; d = value 0, e = counter
                        or      TURBO_MANDATORY | TURBO_LEFT | TURBO_RIGHT
                        ld      b,h                 ; now select chip and set to stereo
                        out     (c),a               ; .
.DefaultLoop:           dec     e                   ; loop down we set E 1 higher
                        jp      z,.Complete         ; On zero we complete, we
                        ld      b,h                 ; Set register to 0
                        out     (c),e               ; .
                        ld      b,l                 ; .
                        out     (c),d               ; .
.DefaultDone:           jp      .DefaultLoop        ; and loop
.Complete:              ld      b,h                 ; we set fine tone to 0
                        out     (c),e               ; though as volume is 0
                        ld      b,l                 ; it doesn't really 
                        out     (c),d               ; matter 
                        ret
                        
;-- Initialise Audio channels to AY1 noise, AY2 and 3 tone, all channels to volume 0

; For each AY channel, set everything to zero, then set up envelope and tone channels.
; for now we are blocking noise channels whilst debugging
InitAudio:              ld      a, TURBO_CHIP_AY1
                        call    DefaultAYChip
                        WriteTurboRegister ENVELOPE_SHAPE,ENVELOPE_SHAPE_SINGLE_ATTACK_HOLD
                        WriteTurboRegister TONE_ENABLE,   NOISE_CHANNEL_A | NOISE_CHANNEL_B | NOISE_CHANNEL_C
                        ld      a, TURBO_CHIP_AY2
                        call    DefaultAYChip
                        WriteTurboRegister ENVELOPE_SHAPE,ENVELOPE_SHAPE_SINGLE_ATTACK_HOLD
                        WriteTurboRegister TONE_ENABLE,   NOISE_CHANNEL_A | NOISE_CHANNEL_B | NOISE_CHANNEL_C
                        ld      a, TURBO_CHIP_AY3
                        call    DefaultAYChip
                        WriteTurboRegister ENVELOPE_SHAPE,ENVELOPE_SHAPE_SINGLE_ATTACK_HOLD
                        WriteTurboRegister TONE_ENABLE,   NOISE_CHANNEL_A | NOISE_CHANNEL_B | NOISE_CHANNEL_C                      
                        ld      hl,SoundChannelSeq      ; now set up all the channel data to $FF
                        ld      hl,SoundChannelSeq      ; now set up all the channel data to $FF
                        ld      a,$FF                   ; which means that it
                        ld      b,8                     ; has no data to play
.InitLoop:              ld      (hl),a
                        inc     hl
                        djnz    .InitLoop
                        ret

;Enable with no noise chanels active
InitAudioMusic:         ld      a, TURBO_CHIP_AY1
                        call    DefaultAYChip
                        WriteTurboRegister ENVELOPE_SHAPE,ENVELOPE_SHAPE_SINGLE_ATTACK_HOLD
                        WriteTurboRegister TONE_ENABLE,   NOISE_CHANNEL_A | NOISE_CHANNEL_B | NOISE_CHANNEL_C
                        ld      a, TURBO_CHIP_AY2
                        call    DefaultAYChip
                        WriteTurboRegister ENVELOPE_SHAPE,ENVELOPE_SHAPE_SINGLE_ATTACK_HOLD
                        WriteTurboRegister TONE_ENABLE,   NOISE_CHANNEL_A | NOISE_CHANNEL_B | NOISE_CHANNEL_C
                        ld      a, TURBO_CHIP_AY3
                        call    DefaultAYChip
                        WriteTurboRegister ENVELOPE_SHAPE,ENVELOPE_SHAPE_SINGLE_ATTACK_HOLD
                        WriteTurboRegister TONE_ENABLE,   NOISE_CHANNEL_A | NOISE_CHANNEL_B | NOISE_CHANNEL_C                      
                        ret
                        
GetSoundAAddressToHL:   MACRO
                        ld      hl,SFXPointerList
                        add     a,a
                        add     hl,a
                        ld      a,(hl)
                        inc     hl
                        ld      h,(hl)
                        ld      l,a
                        ENDM

SetIXToChannelA:        MACRO
                        ld      hl,SoundChannelSeq
                        add     hl,a
                        ld      ix,hl
                        ENDM
                        
SelectChannelMapping:   MACRO
                        ld      hl,SoundChipMapNumber
                        add     hl,a
                        ld      a,(hl)
                        ENDM

; This version ignores nooise and envelope setup so its always 0 atack hold and 
; noise is pre-configured in channel                        
; The channel always holds a pointer to the next block of data to play
PlayChannelD:           ld      a,(ix+SoundDataPointerOffset)    ; set hl to current data block
                        ld      l,a                              ; for SFX step
                        ld      a,(ix+SoundDataPointerOffset1)   ;
                        ld      h,a                              ;
                        ld      a,(hl)                           ; get fine
                        WriteAToTurboRegister d
                        inc     d                                ; Move to channel coarse
                        inc     hl      
                        ld      a,(hl)    
                        WriteAToTurboRegister d                      
                        ld      a,d
                        add     a,7
                        ld      d,a
                        inc     hl                               ; Get Volume
                        ld      a,(hl)
                        WriteAToTurboRegister d                 
                        ret
                        
;--- Take the current sound to play, Put it in a noise or tone channel (if bit 1 is clear is a tone only)
EnqueSound:             ld      a,(SoundFxToEnqueue)                ; Get Sound FX to Enque
                        JumpIfAGTENusng SFXEndOfList, .InvalidSound ; Invalid sounds get discarded quickly
.GetSoundData:          ld      e,a                                 ; save SoundFxToEnqeue
                        and     $01                                 ; even numbers are tone only (Including 0)
                        jr      nz,.FindFreeNoiseChannel         
.FindFreeToneChannel:   ld      hl,SoundChannelSeq + 2              ; so we start at the first tone channel
                        ld      d,$FF                               ; d = marker for free slot cp d will be faster in the loop
                        ld      c,2                                 ; c= current slot
                        ld      b,7                                 ; b = nbr of slots
.ToneScanLoop:          ld      a,(hl)                              ; is channel occupied
                        cp      d
                        jr      z,.SaveSoundId                      ; if its free then move forward
                        inc     c                                   ; c is hunting for a free channel
                        inc     hl                                  ; move tonext address in channel list
                        djnz    .ToneScanLoop
.NoFreeSlot:            ret                                         ; no free slot, leave sound enqued
.FindFreeNoiseChannel:  ld      hl,SoundChannelSeq                  ; We only have 2 noise channels so no need to
                        ld      c,0                                 ; do a complex loop
                        ld      a,(hl)
                        ld      d,$FF                               ; d = marker for free slot
                        cp      d
                        jr      z,.SaveSoundId
                        inc     hl
                        ld      a,(hl)
                        cp      d
.NoNoiseSlot:           ret     nz                                  ; no free slot, leave sound enqued
.NoiseChannel2:         ld      c,1                                 ; So we have channel 1 free                       
.SaveSoundId:           ld      a,e                                 ; get back sound id
                        GetSoundAAddressToHL                        ; hl = pointer to sfx data
                        ex      de,hl                               ; save pointer to data also makes loading to (ix) easier
.SetIXToChannelPointer: ld      hl,SoundChannelSeq                  ; Get the sequence for the 
                        ld      a,c                                 ; respective channel
                        add     hl,a                                ; that we are looking at
                        ld      ix,hl                               ; now we can use indexed access
.GetSFXDataBack:        ex      de,hl                               ; hl = sound fx again
.LoadSeqCount           ZeroA
                        ld      (ix+0),a                            ; set SoundChannelSeq[channel] to 0 as its starting
                        ld      a,(hl)                              ; get the nbr of steps
                        ld      (ix+SoundLastSeqOffset),a           ; load SoundChannelLastSeq[channel]
                        IFDEF   USETIMER
                            ld      a,1                             ; for now we have separate timers, we enque with 1 so the loops starts immediatly
                            ld      (ix+SoundTimerOffset),a         a; load SoundChannelTimer[channel] with duration
                        ENDIF
                        inc     hl                                  ; move hl to first byte of data block
                        ld      (ix+SoundDataPointerOffset),l       ; load SoundDataPointer[channel] with current data set
                        ld      (ix+SoundDataPointerOffset1),h
.InvalidSound:          ld      a,$FF           
                        ld      (SoundFxToEnqueue),a                ; ClearFXEnqeue
                        ret

PlaySound:              ld      e,a                                 ; save channel number
                        SetIXToChannelA                             ; We trap for debugging
.GetCurrentSeq:         ld      a,(ix+0)                            ; for optimisation we 
                        cp      $FF                                 ; will never call this if its $FF
                        ret     z                                   ; its just a belt n braces
;--- Play Next Step, we select chip, select channel, set up tone then step, timer & pointer
.SelectChip             ld      a,(ix+SoundChipMapOffset)           ; get the mapping. bits 1 and 0 hold
                        ld      d,a                                 ; .
                        or      %11111100                           ; .
                        WriteTurboControlA                          ; .
.CheckLastSeq:          ld      a,(ix+SoundLastSeqOffset)           ; get last in sequence
                        JumpIfALTNusng  (ix+0),.CompletedSFX        ; if we have gone beyond last then done                        
.PlayStep:              ld      a,d                                 ; Get the channel number
                        and     %00110000 
                        swapnib                                     ; get channel to lower bits
                        ld      d,a
                        call    PlayChannelD                        ; play channel D step ix is pointer to correct soundchannelseq
.UpdateStep:            inc     (ix+0)                              ; next stepssss
                        IFDEF   USETIMER
.UpdateTimer:               ld      a,SOUNDSTEPLENGTH
                            ld      (ix+SoundTimerOffset),a
                        ENDIF
.UpdateStepPointer:     ld      e,(ix+SoundDataPointerOffset)       ; move pointer on by 7 bytes
                        ld      d,(ix+SoundDataPointerOffset+1)     ;
                        ex      de,hl                               ; hl = current pointer
                        ld      a,SFXBlockLength                    ; move to next block
                        add     hl,a                                ; .
                        ex      de,hl                               ; move to de for load back
                        ld      (ix+SoundDataPointerOffset),e       ;
                        ld      (ix+SoundDataPointerOffset+1),d     ;
                        ret
.CompletedSFX:          ld      a,(ix+SoundChipMapOffset)           ; channel number is in upper bits
                        swapnib                                     ; so we need it in 
                        and     %00000011                           ; lower for selecting volume register
                        add     a,CHANNEL_A_AMPLITUDE               ; select the register
                        WriteTurboRegisterA 0                       ; set volume to 0
                        WriteTurboRegister ENVELOPE_PERIOD_FINE,0
                        WriteTurboRegister ENVELOPE_PERIOD_COARSE,0                        
                        WriteTurboRegister ENVELOPE_SHAPE,ENVELOPE_SHAPE_SINGLE_ATTACK_HOLD
                        WriteTurboRegister TONE_ENABLE,   NOISE_CHANNEL_A | NOISE_CHANNEL_B | NOISE_CHANNEL_C
                        ld      a,$FF                               ; set sequence to FF to denote
                        ld      (ix+0),a                            ; channel is now free
                        ret

; Engine Sound is always a priority so gets a dedicated channel
; this is only called if delta has changed
UpdateEngineSound:      SelectAY TURBO_CHIP_AY1
.SetUpTone:             ld      a,(DELTA)   
                        and     a
                        jp      z,.EngineOff    ; if speed is 0 the engine off as a = 0
                        ld      hl,$08F3        ; base tone - delta * 15
                        ld      d,a             ; we subtract as the tone is the 
                        ld      e,15            ; time between pulses
                        sub     hl,de
                        ld      a,CHANNEL_A_FINE
                        ld      bc,$FFFD
                        out     (c),a
                        ld      b,$BF
                        out     (c),h
                        inc     a
                        ld      b,$FF
                        out     (c),a
                        ld      b,$BF
                        out     (c),l
.SetUpNoise:            ld      a,(DELTA)       ; l = DELTA / 4
                        srl     a
                        ld      d,a             ; 
                        srl     a
                        srl     a               ; a = DELTA / 8
                        add     a, $1F          ; more noise higher the speed
                        ld      b,$FF
                        ld      e,NOISE_PERIOD
                        out     (c),e
                        ld      b,$BF
                        out     (c),a
                        ld      a,d             ; get back delta / 4
                        add     a,5
                        srl     a               ; calculate a scaled from 2 to 7
.EngineOff:             ld      e,CHANNEL_A_AMPLITUDE
                        ld      b,$FF
                        out     (c),e
                        ld      b,$BF
                        out     (c),a
                        WriteTurboRegister ENVELOPE_PERIOD_FINE,0
                        WriteTurboRegister ENVELOPE_PERIOD_COARSE,0
                        WriteTurboRegister ENVELOPE_SHAPE,ENVELOPE_SHAPE_SINGLE_ATTACK_HOLD
                        WriteTurboRegister TONE_ENABLE,   NOISE_CHANNEL_A | NOISE_CHANNEL_B | NOISE_CHANNEL_C

                        ld      a,(DELTA)
                        ld      (LAST_DELTA),a
                        ret

SoundLabel              DB      "Sound Channels  "
SoundChannelSeq         DS      8   ; The current step in the SFX or $FX for empty
SoundChannelLastSeq     DS      8   ; A copy of SFX length to save an extra lookup, $FF means 1 step always on, $00 means off
SoundChannelTimer       DS      8   ; The count down to next sequence
SoundDataPointer        DS      2*8 ; pointer to current sound step
;                  Channel/Chip B/1  C/1  A/2  B/2  C/2  A/3  B/3  C/3
SoundChipMapNumber      DB      $10 | TURBO_CHIP_AY1, $20 | TURBO_CHIP_AY1
                        DB      $00 | TURBO_CHIP_AY2, $10 | TURBO_CHIP_AY2 | $20 | TURBO_CHIP_AY2
                        DB      $00 | TURBO_CHIP_AY2, $10 | TURBO_CHIP_AY3 | $20 | TURBO_CHIP_AY3
SoundLastSeqOffset      EQU     8
SoundTimerOffset        EQU     SoundLastSeqOffset + 8
SoundDataPointerOffset  EQU     SoundTimerOffset + 8
SoundDataPointerOffset1 EQU     SoundTimerOffset + 9
SoundChipMapOffset      EQU     SoundDataPointerOffset + 16
;Chip map is            bits 5,4 channel letter A = 0 B =1 C = 2    1,0 Chip Number
;Mapping                0 = 1B, 1 = 1C, 3=2A, 4 = 2B, 5=2C, 6 = 3A, 7 = 3B, 8 = 3C
;                       Chip 1 Engine, noise channel but engine noise period is priority
;                       Chip 2 General FX, no noise
;                       Chip 3 General FX, no noise

;--- Data sets for Sound
;--- Sound Channels are 0 to 9 AY1 A B C AY2 A B C AY3 A B C
;--- Sounds can not enqueu, the find a free slot & SoundFxtoEnqueue gets zeroed or it gets left
;--- AY1 channel 1 is reserved for engine noise as its calculated on the fly
; Predefined value for each channel to load to $FFFD before setting up tone
; Hcops a copy of the 3 chips register 7


; StepLength            DB      1   ; $FF = 1 step forever else number of entries in StepListArray
; StepListArray
;     TonePitch         DW      4 bits unused + 12 bits
;     NoisePitch        DB      5 bit or $FF for no noise
;     Volume            DB      0 to 15
;     EnvelopePeriod    DW      0
;     WaveForm          DB      0

; SFX Format
; SFXPointerList - Odd numbers are tone, Even Numbers have noise too, optimisation for channel handling
SoundLabelSFX   DB      "Sound Data      "
SFXPointerList  dw      SFXLaser
SFXEndOfList    EQU     1
SFXBlockLength  EQU     3
SFXFineOffset   EQU     0
SFXCorseOffset  EQU     1
SFXVolOffset    EQU     2
;                       Step Count
SoundLaser      DB      "Laser  "    
SFXLaser        db      11
;                       Tone 
;                      Fine Crs  Vol 
               db      $5F, $00, $0E
               db      $39, $00, $0E
               db      $47, $00, $0D
               db      $5E, $00, $0C
               db      $6E, $10, $0A
               db      $76, $00, $08
               db      $76, $00, $07
               db      $06, $01, $06
               db      $2E, $01, $03
               db      $16, $01, $03
               db      $03, $00, $01

;
;
;Laser1:          db 14,14,13,12,10, 8, 7, 6, 3, 1
;LaserFrameCount  db  5, 5, 5, 5, 5, 5, 5, 5, 5, 5
;LaserLength:     db $-LaserFrameCount
;LaserTone:       dw $05F, $089, $097, $0AE, $0CE, $0B6, $0E6, $106, $13E, $126, $136

DanubePointer1:         DW BlueDanube1
DanubePointer2:         DW BlueDanube2
DanubePointer3:         DW BlueDanube3
DanubePointer4:         DW BlueDanube4
DanubePointer5:         DW BlueDanube5
DanubePointer6:         DW BlueDanube6
DanubeCounter:          DB 0 
DanubeMax:              EQU BlueDanube2 - BlueDanube1
DanubeMaxMem:           DB  DanubeMax
DanubeVolume:           DB $08
DanubePace:             DB 17
DanubeTimer:            DB 17

SelectDanubeAYa:        or TURBO_MANDATORY | TURBO_LEFT | TURBO_RIGHT
                        WriteTurboControlA
                        ret

WriteAToTurboRegisterD: ex      af,af'
                        ld      a,d
                        WriteTurboControlA
                        ld      b,$BF
                        ex      af,af'
                        out     (c),a
                        ret
                        
SetChannelAVolume0:     WriteTurboControlA
                        ld      b,$BF
                        ZeroA
                        out     (c),a
                        ret

SetChannelDNoteAtHL:    ld      a,d                 ; a is now 0 to 2
                        sla     a                   ; multiply by 2 so now 0, 2, 4
                        push    af                  ; save fine adjust register nbr
                        WriteTurboControlA          ; Send out fine adjust
                        ld      b,$BF
                        ld      a,(hl)              ; get fine note
                        out     (c),a               ; write fine note
                        pop     af                  ; get back fine adjust register
                        inc     a
                        WriteTurboControlA
                        inc     hl                  ; move to coarse value
                        ld      b,$BF
                        ld      a,(hl)
                        out     (c),a
.SetVolume:             ld      a,d                 ; get back channel
                        add     a,8
                        WriteTurboControlA
                        ld      b,$BF
                        ld      a,(DanubeVolume)    ; could put in ixh but we have plenty of cycles
                        out     (c),a
                        ret
               
PlayDanubeNote:         and     a
                        jr      z,  .NoNote
                        ld      hl, TonesPitch
                        ld      e,a         ; save a
                        and     $F0         ; load octave to d
                        swapnib             ;
                        ld      d,a         ;
                        ld      a,e         ; get a back
                        ld      e,12        ; 12 semi tones so mul by 12
                        mul                 ;
                        and     $0F         ; get semi tone
                        ex      de,hl       ; hl = octave * 12 + semi tone
                        add     hl,a        ; .
                        ShiftHLLeft1        ; multiply by 2 as we have 2 bytes per tone
                        ld      de,TonesPitch
                        add     hl,de       ; now we are pointing at table
                        ld      d,b         ; get channel number
                        call    SetChannelDNoteAtHL
                        ret
.NoNote:                ld      a,b         ; a= channel number 0 to 3
                        add     8           ; adjust to volume register
.NoNoteOK               call    SetChannelAVolume0
                        ret

PlayDanube:             ld      a,(DanubeTimer)
                        dec     a
                        jr      z,.PlaySequence
                        ld      (DanubeTimer),a
                        ret
.PlaySequence:  
.PlayChannel1:          ld      a,TURBO_CHIP_AY2
                        call    SelectDanubeAYa
                        ld      hl,(DanubePointer1)
                        ld      a,(hl)
                        ld      b,0
                        call    PlayDanubeNote
.PlayChannel2:          ld      a,TURBO_CHIP_AY2
                        call    SelectDanubeAYa
                        ld      hl,(DanubePointer2)
                        ld      a,(hl)
                        ld      b,2
                        call    PlayDanubeNote
.PlayChannel3:          ld      a,TURBO_CHIP_AY2
                        call    SelectDanubeAYa
                        ld      hl,(DanubePointer3)
                        ld      a,(hl)
                        ld      b,2
                        call    PlayDanubeNote
.PlayChannel4:          ld      a,TURBO_CHIP_AY3
                        call    SelectDanubeAYa
                        ld      hl,(DanubePointer4)
                        ld      a,(hl)
                        ld      b,0
                        call    PlayDanubeNote
.PlayChannel5:          ld      a,TURBO_CHIP_AY3
                        call    SelectDanubeAYa
                        ld      hl,(DanubePointer5)
                        ld      a,(hl)
                        ld      b,1
                        call    PlayDanubeNote
.PlayChannel6:          ld      a,TURBO_CHIP_AY3
                        call    SelectDanubeAYa
                        ld      hl,(DanubePointer6)
                        ld      a,(hl)
                        ld      b,2
                        call    PlayDanubeNote                        
.UpdatePointers:        ld      a,(DanubePace)
                        ld      (DanubeTimer),a
                        ld      a,(DanubeCounter)
                        cp      DanubeMax
                        ;break
                        jr      nz,.MovePointersForward
.ResetPointers:         ld      hl,BlueDanube1
                        ld      (DanubePointer1),hl
                        ld      hl,BlueDanube2
                        ld      (DanubePointer2),hl
                        ld      hl,BlueDanube3
                        ld      (DanubePointer3),hl
                        ld      hl,BlueDanube4
                        ld      (DanubePointer4),hl
                        ld      hl,BlueDanube5
                        ld      (DanubePointer5),hl
                        ld      hl,BlueDanube6
                        ld      (DanubePointer6),hl
                        ld      a,0
                        ld      (DanubeCounter),a
.MovePointersForward:   inc     a
                        ld      (DanubeCounter),a
                        ld      hl,DanubePointer1
                        call    AdvancePointer
                        ld      hl,DanubePointer2
                        call    AdvancePointer
                        ld      hl,DanubePointer3
                        call    AdvancePointer
                        ld      hl,DanubePointer4
                        call    AdvancePointer
                        ld      hl,DanubePointer5
                        call    AdvancePointer
                        ld      hl,DanubePointer6
                        call    AdvancePointer
                        ret

AdvancePointer:         ld      a,(hl)
                        inc     hl
                        ld      d,(hl)
                        ld      e,a
                        inc     de
                        ld      (hl),d
                        dec     hl
                        ld      (hl),e
                        ret
                        
                        

                 
TonesPitch       db $C8, $1F        ; Octave 0 (I think base note is A currently so this is all skewed
                 db $00, $1E        ;
                 db $90, $1D        ;
                 db $5D, $0D        ;
                 db $9C, $0C        ;
                 db $E7, $0B        ;
                 db $3C, $0B        ;
                 db $9B, $0A        ;
                 db $02, $0A        ;
                 db $73, $09        ;
                 db $EB, $08        ;
                 db $6B, $08        ;
                 db $F2, $07        ; Octave 1
                 db $80, $07        ;
                 db $64, $07        ;
                 db $AE, $06        ;
                 db $4E, $06        ;
                 db $F3, $05        ;
                 db $9E, $05        ;
                 db $4D, $05        ;
                 db $01, $05        ;
                 db $B9, $04        ;
                 db $75, $04        ;
                 db $35, $04        ;
                 db $F9, $03        ; Octave 2
                 db $C0, $03        ;
                 db $B2, $03        ;
                 db $57, $03        ;
                 db $27, $03        ;
                 db $F9, $02        ;
                 db $CF, $02        ;
                 db $A6, $02        ;
                 db $80, $02        ;
                 db $5C, $02        ;
                 db $3A, $02        ;
                 db $1A, $02        ;
                 db $FC, $01        ; Octave 3
                 db $E0, $01        ;
                 db $D9, $01        ;
                 db $AB, $01        ;
                 db $93, $01        ;
                 db $7C, $01        ;
                 db $67, $01        ; First note?
                 db $53, $01        ;
                 db $40, $01        ;
                 db $2E, $01        ;
                 db $1D, $01        ;
                 db $0D, $01        ;
                 db $FE, $00        ; Octave 4
                 db $F0, $00        ;
                 db $EC, $00        ;
                 db $D5, $00        ;
                 db $C9, $00        ;
                 db $BE, $00        ;
                 db $B3, $00        ;
                 db $A9, $00        ;
                 db $A0, $00        ;
                 db $97, $00        ;
                 db $8E, $00        ;
                 db $86, $00        ;
                 db $7F, $00        ; Octave 5
                 db $78, $00        ;
                 db $76, $00        ;
; 1 Byte high nibble Octave, low note 
; for now middle octave 3, 
; low nibble mapping
;       0      1  2  3  4  5  6   7  8  9   A  B  C
;       None   A  A# B  C  C# D  D#  E  F  F#  G  G#
;BlueDanube1:     DB $00, $00, $00, $00, $00, $61, $61, $00, $00, $00, $00, $00, $00, $00, $00, $00, $00, $61, $61, $5B, $5B, $00, $00, $00
;BlueDanube2:     DB $00, $00, $00, $00, $00, $5A, $5A, $5A, $5A, $00, $00, $00, $00, $00, $00, $00, $00, $58, $58, $55, $55, $00, $00, $00
;BlueDanube3:     DB $00, $00, $00, $41, $41, $00, $00, $56, $56, $00, $00, $00, $00, $00, $00, $41, $41, $00, $00, $00, $00, $00, $00, $00
;BlueDanube4:     DB $36, $36, $3A, $00, $00, $00, $00, $00, $00, $00, $00, $00, $36, $36, $3A, $00, $00, $00, $00, $00, $00, $00, $00, $00

BlueDanube1:          DB 	$00, 	$00, 	$38, 	$00, 	$00, 	$3A, 	$3A, 	$3A, 	$00, 	$00, 	$38, 	$00, 	$00, 	$3B, 	$3B, 	$3B, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00
                      DB 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$38, 	$38, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$38, 	$38, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$3B, 	$3B, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$3B, 	$3B, 	$00, 	$38, 	$38, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$3A, 	$3A, 	$3A, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$38, 	$00, 	$00, 	$3A, 	$00, 	$00, 	$3B, 	$3B, 	$00, 	$3B, 	$39																																							
BlueDanube2:          DB 	$34, 	$36, 	$00, 	$00, 	$34, 	$34, 	$34, 	$34, 	$34, 	$36, 	$00, 	$00, 	$33, 	$33, 	$33, 	$33, 	$33, 	$34, 	$36, 	$00, 	$00, 	$34, 	$00, 	$00, 	$34, 	$00, 	$00, 	$34, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$36, 	$36, 	$00, 	$33, 	$33, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$36, 	$36, 	$00
                      DB 	$00, 	$34, 	$34, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$34, 	$34, 	$00, 	$34, 	$34, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$33, 	$33, 	$00, 	$33, 	$33, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$33, 	$33, 	$00, 	$36, 	$36, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$38, 	$38, 	$00, 	$34, 	$34, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$34, 	$34, 	$34, 	$00, 	$00, 	$33, 	$00, 	$00, 	$33, 	$00, 	$00, 	$00, 	$34, 	$00, 	$00, 	$34, 	$00, 	$00, 	$33, 	$33, 	$00, 	$33, 	$00																																								
BlueDanube3:          DB 	$00, 	$00, 	$00, 	$00, 	$00, 	$31, 	$31, 	$31, 	$00, 	$00, 	$00, 	$00, 	$00, 	$2B, 	$2B, 	$2B, 	$00, 	$00, 	$00, 	$00, 	$31, 	$00, 	$00, 	$2B, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$31, 	$00, 	$00, 	$2B, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$33, 	$33, 	$00, 	$2B, 	$2B, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$31, 	$31, 	$00
                      DB 	$00, 	$31, 	$31, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$31, 	$31, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$2B, 	$2B, 	$00, 	$00, 	$00, 	$00, 	$00, 	$2B, 	$2B, 	$2B, 	$2B, 	$00, 	$2B, 	$2B, 	$00, 	$00, 	$00, 	$00, 	$2B, 	$00, 	$00, 	$2B, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$31, 	$31, 	$31, 	$00, 	$00, 	$00, 	$2B, 	$2B, 	$00, 	$2B, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$2B, 	$2B, 	$00, 	$00, 	$2C																																								
BlueDanube4:          DB 	$00, 	$00, 	$00, 	$26, 	$00, 	$26, 	$26, 	$26, 	$00, 	$00, 	$00, 	$26, 	$00, 	$26, 	$26, 	$26, 	$00, 	$00, 	$00, 	$26, 	$26, 	$00, 	$26, 	$26, 	$00, 	$26, 	$2C, 	$00, 	$26, 	$28, 	$00, 	$00, 	$26, 	$00, 	$00, 	$00, 	$2C, 	$00, 	$26, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$26, 	$26, 	$26, 	$26, 	$26, 	$26, 	$26, 	$26, 	$00, 	$00, 	$00, 	$00, 	$26, 	$26, 	$26, 	$26, 	$26, 	$26
                      DB 	$26, 	$26, 	$26, 	$00, 	$00, 	$00, 	$00, 	$28, 	$28, 	$28, 	$28, 	$28, 	$28, 	$28, 	$28, 	$00, 	$00, 	$00, 	$00, 	$28, 	$28, 	$28, 	$28, 	$28, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$26, 	$26, 	$26, 	$26, 	$26, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$26, 	$28, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$28, 	$28, 	$00, 	$00, 	$00, 	$00, 	$26, 	$00, 	$26, 	$26, 	$26, 	$00, 	$00, 	$00, 	$00, 	$00, 	$28, 	$00, 	$26, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00																																							
BlueDanube5:          DB 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$24, 	$00, 	$00, 	$23, 	$24, 	$00, 	$21, 	$00, 	$00, 	$00, 	$24, 	$00, 	$00, 	$21, 	$00, 	$00, 	$00, 	$23, 	$00, 	$23, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$23, 	$00, 	$21, 	$00, 	$00, 	$00, 	$00
                      DB 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$21, 	$00, 	$21, 	$00, 	$00, 	$00, 	$00, 	$00, 	$21, 	$00, 	$00, 	$00, 	$21, 	$00, 	$23, 	$23, 	$23, 	$23, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$23, 	$00, 	$23, 	$23, 	$23, 	$23, 	$00, 	$23, 	$23, 	$00, 	$00, 	$23, 	$00, 	$24, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$21, 	$21, 	$24, 	$00, 	$00, 	$00, 	$00, 	$00, 	$25, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$23, 	$23, 	$00, 	$21, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00																																								
BlueDanube6:          DB 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$1B, 	$00, 	$00, 	$1B, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$1B, 	$1B, 	$00, 	$00, 	$1A, 	$00, 	$00, 	$00, 	$00
                      DB 	$00, 	$00, 	$00, 	$00, 	$1A, 	$1A, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$1A, 	$1A, 	$00, 	$00, 	$1B, 	$1B, 	$1B, 	$1B, 	$00, 	$00, 	$00, 	$00, 	$1B, 	$1B, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$1B, 	$1B, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$1B, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$00, 	$1B, 	$00, 	$00, 	$00, 	$00, 	$00																																								
